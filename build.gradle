/*
   Copyright 2018 Sam Pritchard

   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at

       http://www.apache.org/licenses/LICENSE-2.0

   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License.
 */
buildscript {
    ext {
        kotlin_version = '1.2.51'
        dokka_version = '0.9.17'
        shadow_version = '2.0.4'
        versions_version = '0.20.0'
    }
    repositories {
        jcenter()
        mavenCentral()
        maven {url 'https://plugins.gradle.org/m2/'}
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
        classpath "com.github.jengelman.gradle.plugins:shadow:$shadow_version"
        classpath "com.github.ben-manes:gradle-versions-plugin:$versions_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'idea'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'com.github.ben-manes.versions'

group 'basalt'
version '0.4.0'
description = 'A fully-documented and fast client implementation for Basalt and JDA.'
sourceCompatibility = 10
targetCompatibility = 10

repositories {
    jcenter()
    mavenCentral()
    maven {url 'https://repo.spring.io/milestone'}
}

dokka {
    moduleName = 'BasaltClient'
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
    includeNonPublic = true
    jdkVersion = 8
    linkMapping {
        dir = 'src/main/kotlin'
        url = 'https://github.com/SamOphis/BasaltClient/blob/master/src/main/kotlin'
        suffix = '#L'
    }
}

shadowJar {
    classifier = 'withDependencies'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: dokka) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task updateDocs(type: Copy, dependsOn: javadocJar) {
    from "$buildDir/docs/javadoc"
    into "docs"
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.9'
}

build {
    dependsOn clean
    dependsOn sourcesJar
    dependsOn javadocJar
    dependsOn shadowJar
    dependsOn updateDocs

    sourcesJar.mustRunAfter clean
    javadocJar.mustRunAfter sourcesJar
    shadowJar.mustRunAfter javadocJar
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

ext {
    websocket_version = '2.5'
    slf4j_version = '1.7.25'
    lavaplayer_version = '1.3.7'
    jsoniter_version = '0.9.23'
    javassist_version = '3.23.1-GA'
    fastutil_version = '8.2.1'
    jda_version = '3.7.1_386'
    reactor_core_version = '3.2.0.M2'
}

dependencies {
    compile group: 'com.neovisionaries', name: 'nv-websocket-client', version: websocket_version
    compile group: 'org.slf4j', name: 'slf4j-api', version: slf4j_version
    compile group: 'com.sedmelluq', name: 'lavaplayer', version: lavaplayer_version
    compile group: 'com.jsoniter', name: 'jsoniter', version: jsoniter_version
    compile group: 'org.javassist', name: 'javassist', version: javassist_version
    compile group: 'it.unimi.dsi', name: 'fastutil', version: fastutil_version
    compile group: 'net.dv8tion', name: 'JDA', version: jda_version
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8', version: kotlin_version
    compile group: 'io.sentry', name: 'sentry-logback', version: '1.7.5' // REMOVE FOR PRODUCTION
    compile group: 'io.projectreactor', name: 'reactor-core', version: reactor_core_version
}

compileKotlin {
    kotlinOptions {
        jvmTarget = '1.8'
    }
}
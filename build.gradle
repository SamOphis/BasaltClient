/*
   Copyright 2018 Sam Pritchard

   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at

       http://www.apache.org/licenses/LICENSE-2.0

   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License.
 */

buildscript {
    ext {
        kotlin_version = '1.2.71'
        dokka_version = '0.9.17'
        shadow_version = '2.0.4'
        versions_version = '0.20.0'
        bintray_version = '1.8.4'
    }
    repositories {
        jcenter()
        mavenCentral()
        maven {url 'https://plugins.gradle.org/m2/'}
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
        classpath "com.github.jengelman.gradle.plugins:shadow:$shadow_version"
        classpath "com.github.ben-manes:gradle-versions-plugin:$versions_version"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:$bintray_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'idea'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

group 'com.github.samophis'
version '0.9.0'
description = 'A fast, reactive client implementation for Basalt and JDA.'
sourceCompatibility = 10
targetCompatibility = 10

repositories {
    jcenter()
    mavenCentral()
    maven { url 'https://repo.spring.io/milestone' }
}

dokka {
    moduleName = 'BasaltClient'
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
    includeNonPublic = true
    jdkVersion = 8
    linkMapping {
        dir = 'src/main/kotlin'
        url = 'https://github.com/SamOphis/BasaltClient/blob/master/src/main/kotlin'
        suffix = '#L'
    }
}

bintray {
    user = bintrayUsername
    key = bintrayApiKey
    publications = ['BintrayRelease']
    pkg {
        repo = 'maven'
        name = 'BasaltClient'
        licenses = ['Apache-2.0']
        vcsUrl = ['https://github.com/SamOphis/BasaltClient.git']
        publish = true
        version {
            name = project.version
            released = new Date()
        }
    }
}

publishing {
    publications {
        BintrayRelease(MavenPublication) {
            from components.java
            groupId group
            artifactId 'BasaltClient'
            version version
            artifact javadocJar
            artifact sourcesJar
        }
    }
}


shadowJar {
    classifier = 'withDependencies'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: dokka) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task updateDocs(type: Copy, dependsOn: javadocJar) {
    from "$buildDir/docs/javadoc"
    into "docs"
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.10'
}

build {
    dependsOn clean
    dependsOn jar
    dependsOn sourcesJar
    dependsOn javadocJar
    dependsOn shadowJar
    dependsOn updateDocs
    dependsOn test

    jar.mustRunAfter clean
    javadocJar.mustRunAfter jar
    sourcesJar.mustRunAfter javadocJar
    shadowJar.mustRunAfter sourcesJar
}

bintrayUpload {
    dependsOn build

    onlyIf { !getProjectProperty("bintrayUsername").empty }
    onlyIf { !getProjectProperty("bintrayApiKey").empty }
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

ext {
    slf4j_version = '1.7.25'
    lavaplayer_version = '1.3.7'
    jsoniter_version = '0.9.23'
    javassist_version = '3.23.1-GA'
    trove_version = '3.0.3'
    jda_version = '3.8.0_428'
    vertx_version = '3.5.4'
    vertx_futures_version = '0.1.2'
}

dependencies {
    compile group: 'org.slf4j', name: 'slf4j-api', version: slf4j_version
    compileOnly group: 'com.sedmelluq', name: 'lavaplayer', version: lavaplayer_version
    compile group: 'com.jsoniter', name: 'jsoniter', version: jsoniter_version
    compile group: 'org.javassist', name: 'javassist', version: javassist_version
    compile group: 'net.sf.trove4j', name: 'trove4j', version: trove_version // replacing fastutil bc 2.4mb vs 19mb
    compileOnly group: 'net.dv8tion', name: 'JDA', version: jda_version
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8', version: kotlin_version
    compile group: 'io.vertx', name: 'vertx-lang-kotlin', version: '3.5.4'
    compile group: 'me.escoffier.vertx', name: 'vertx-completable-future', version: vertx_futures_version
}

compileKotlin {
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

String getProjectProperty(String propertyName)
{
    String property = ""
    if (hasProperty(propertyName))
    {
        property = this.properties[propertyName]
    }
    return property
}